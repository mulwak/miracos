; -------------------------------------------------------------------
;                        MIRACOS INTERRUPT
; -------------------------------------------------------------------
; BCOSの割り込み関連部分
; -------------------------------------------------------------------
; --- BCOS独自の割り込みハンドラ ---
IRQ_BCOS:
; SEIだけされてここに飛んだ
; --- 外部割込み判別 ---
  PHA ; まだXY使用禁止
  ; UART判定
  LDA UART::STATUS
  BIT #%00001000
  BEQ @SKP_UART       ; bit3の論理積がゼロ、つまりフルじゃない
  JMP BCOS_UART::IRQ
@SKP_UART:
  ; VIA判定
  LDA VIA::IFR        ; 割り込みフラグレジスタ読み取り
  LSR                 ; C = bit 0 CA2
  BCC @SKP_CA2
  ; 垂直同期割り込み処理
  PHX
  PHY
  ; NOTE:ここにキーボード処理など
  JMP (VBLANK_USER_VEC16)
@SKP_CA2:

; 不明な割り込みはデバッガへ
DONKI:
  PLA
  JMP DONKI::ENT_DONKI

; 何もせずに垂直同期割り込みを終える
; デフォルトでVBLANK_USER_VEC16に登録される
VBLANK_STUB:
  LDA VIA::IFR
  AND #%00000001      ; 割り込みフラグを折る
  STA VIA::IFR
  PLY
  PLX
  PLA
  CLI
  RTI

; -------------------------------------------------------------------
; BCOS 19             垂直同期割り込みハンドラ登録
; -------------------------------------------------------------------
; input   : AY = ptr
; output  : AY = 垂直同期割り込みスタブルーチン
;                 ルーチンの終わりにジャンプして片付けをやらせたり、
;                 これを登録してハンドラ登録を抹消したりしてよい
; -------------------------------------------------------------------
FUNC_IRQ_SETHNDR_VB:
  storeAY16 VBLANK_USER_VEC16
  loadAY16 IRQ::VBLANK_STUB
  RTS


